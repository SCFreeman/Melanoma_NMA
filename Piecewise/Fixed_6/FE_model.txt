# Fixed treatment effect model

model{
  
  for (i in 1:N){
    d[i]~dpois(mu[i]) #likelihood
    
   A[i] <- alpha[1]*trt2[i] + alpha[2]*trt3[i] + alpha[3]*trt4[i] 
    + alpha[4]*trt5[i] + alpha[5]*trt6[i] + alpha[6]*trt7[i] 
    + alpha[7]*trt8[i] + alpha[8]*trt9[i] + alpha[9]*trt10[i] 
    + alpha[10]*trt11[i] + alpha[11]*trt12[i] + alpha[12]*trt13[i] 
    
   B[i] <- beta[trial[i],spgrp[i]] + log(y[i])
    + phi[1]*trt2[i]*z1[i] + phi[2]*trt3[i]*z1[i] + phi[3]*trt4[i]*z1[i] + phi[4]*trt5[i]*z1[i]
    + phi[5]*trt6[i]*z1[i] + phi[6]*trt7[i]*z1[i] + phi[7]*trt8[i]*z1[i] + phi[8]*trt9[i]*z1[i]
    + phi[9]*trt10[i]*z1[i] + phi[10]*trt11[i]*z1[i] + phi[11]*trt12[i]*z1[i] + phi[12]*trt13[i]*z1[i] 

   log(mu[i]) <- A[i] + B[i] 
  }
  
  ### Priors ###

  for (p in 1:J){
    for (q in 1:ints){
      beta[p,q]~dnorm(0.0, 0.0001)
    }
  }
  
  for(j in 1:12) {
    alpha[j] ~ dnorm(0, 0.0001)
  }
  
  for(j in 1:12) {
    phi[j] ~ dnorm(0, 0.01)
  }


# Calculate survival using CheckMate 066 as baseline

  for(u in 1:1){
    log(hazard[1,u]) <- beta[3,u]   # DTIC
    log(hazard[2,u]) <- beta[3,u] + alpha[1]  # TRL
    log(hazard[3,u]) <- beta[3,u] + alpha[2]  # IPI+DTIC
    log(hazard[4,u]) <- beta[3,u] + alpha[3]  # DB
    log(hazard[5,u]) <- beta[3,u] + alpha[3] + alpha[4]  # DB+TR 
    log(hazard[6,u]) <- beta[3,u] + alpha[5]  # VM
    log(hazard[7,u]) <- beta[3,u] + alpha[5] + alpha[6]  # VM + COB
    log(hazard[8,u]) <- beta[3,u] + alpha[8] - alpha[7]  # IPI
    log(hazard[9,u]) <- beta[3,u] + alpha[8]  # NIV
    log(hazard[10,u]) <- beta[3,u] + alpha[8] - alpha[7] + alpha[9]  # NIV+IPI
    log(hazard[11,u]) <- beta[3,u] + alpha[8] - alpha[7] + alpha[10]# PEM
    log(hazard[12,u]) <- beta[3,u] + alpha[8] - alpha[7] + alpha[11] # IPI+SRG
    log(hazard[13,u]) <- beta[3,u] + alpha[12] # SEL+DTIC
  }
  
  for(u in 2:5){
    log(hazard[1,u]) <- beta[3,u]    # DTIC
    log(hazard[2,u]) <- beta[3,u] + alpha[1] + phi[1]  # TRL
    log(hazard[3,u]) <- beta[3,u] + alpha[2] + phi[2 ] # IPI+DTIC
    log(hazard[4,u]) <- beta[3,u] + alpha[3] + phi[3]  # DB
    log(hazard[5,u]) <- beta[3,u] + alpha[3] + alpha[4] + phi[4]  # DB+TR 
    log(hazard[6,u]) <- beta[3,u] + alpha[5] + phi[5]  # VM
    log(hazard[7,u]) <- beta[3,u] + alpha[5] + alpha[6] + phi[6]  # VM + COB
    log(hazard[8,u]) <- beta[3,u] + alpha[8] - alpha[7] + phi[7]  # IPI
    log(hazard[9,u]) <- beta[3,u] + alpha[8] + phi[8]  # NIV
    log(hazard[10,u]) <- beta[3,u] + alpha[8] - alpha[7] + alpha[9]  + phi[9] # NIV+IPI
    log(hazard[11,u]) <- beta[3,u] + alpha[8] - alpha[7] + alpha[10] + phi[10] # PEM
    log(hazard[12,u]) <- beta[3,u] + alpha[8] - alpha[7] + alpha[11] + phi[11] # IPI+SRG
    log(hazard[13,u]) <- beta[3,u] + alpha[12] + phi[12] # SEL+DTIC
  }

# Months 1-6
for(k in 1:ntrt){
 for(t in 1:6) {
      Cum_H[k,t] <- t*hazard[k, 1] # Cumulative hazard over time by treatment
      TT[k,t] <- 1 - exp(-Cum_H[k,t]) # mortality over time by treatment
      S[k,t] <- 1 - TT[k,t] # Survival over time by treatment
  }
}

# Months 7-12
for(k in 1:ntrt){
 for(t in 7:12) {
      Cum_H[k,t] <- Cum_H[k,6] + ((t-6)*hazard[k,2]) # Cumulative hazard over time by treatment 
      TT[k,t] <- 1 - exp(-Cum_H[k,t]) # mortality over time by treatment
      S[k,t] <- 1 - TT[k,t] # Survival over time by treatment
  }
}

# Months 13-18
for(k in 1:ntrt){
	for(t in 13:18) {
      Cum_H[k,t] <- Cum_H[k,12] + ((t-12)*hazard[k,3]) # Cumulative hazard over time by treatment 
      TT[k,t] <- 1 - exp(-Cum_H[k,t]) # mortality over time by treatment
      S[k,t] <- 1 - TT[k,t] # Survival over time by treatment
  }
}

# Months 19-24
for(k in 1:ntrt){
 for(t in 19:24) {
      Cum_H[k,t] <- Cum_H[k,18] + ((t-18)*hazard[k,4]) # Cumulative hazard over time by treatment 
      TT[k,t] <- 1 - exp(-Cum_H[k,t]) # mortality over time by treatment
      S[k,t] <- 1 - TT[k,t] # Survival over time by treatment
  }
}

# Months 25-120
for(k in 1:ntrt){
 for(t in 25:120) {
      Cum_H[k,t] <- Cum_H[k,24] + ((t-24)*hazard[k,5]) # Cumulative hazard over time by treatment
      TT[k,t] <- 1 - exp(-Cum_H[k,t]) # mortality over time by treatment
      S[k,t] <- 1 - TT[k,t] # Survival over time by treatment
  }
}

# Rank treatments at 60 months
for (l in 1:ntrt) {   
    for (m in 1:ntrt) {    
      rk60[l,m] <- equals(ranked(S[,60],m),S[l,60])     
    }   
  }
  
  }